plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

group 'com.github.kayjamlang'
archivesBaseName = "executor"
version '0.1.3.21'

dependencies {
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    api 'com.github.kayjamlang:core:0.1.3.6'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/service/local/repositories/releases/content/"
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.github.kayjamlang'
            artifactId = project.archivesBaseName
            version = project.version

            pom {
                name = 'KayJam Executor'
                description = 'Provided by the KayJam code executor'
                url = 'https://github.com/KayJamLang/executor'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'levkopo'
                        name = 'Lev Koporushkin'
                        email = 'levkopoag@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/KayJamLang/executor.git'
                    developerConnection = 'scm:git:ssh://github.com/KayJamLang/executor.git'
                    url = 'https://github.com/KayJamLang/executor'
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.findProperty('sonatypeUsername') ?: System.getenv('sonatypeUsername')
                password = project.findProperty('sonatypePassword') ?: System.getenv('sonatypePassword')
            }
        }

        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/KayJamLang/"+project.archivesBaseName)
            credentials {
                username = project.findProperty('githubUsername') ?: System.getenv('githubUsername')
                password = project.findProperty('githubKey') ?: System.getenv('githubKey')
            }
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

signing {
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
        showExceptions = true
        showCauses = true
    }
}